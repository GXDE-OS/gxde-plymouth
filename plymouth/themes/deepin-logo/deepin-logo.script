# Copyright (C) 2011 ~ 2012 Deepin, Inc.
#               2011 ~ 2012 Hou Shaohui
# 
# Author:     Hou Shaohui <houshao55@gmail.com>
# Maintainer: Hou Shaohui <houshao55@gmail.com>
# 
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# Set Background Color
Window.SetBackgroundTopColor(0, 0, 0);
Window.SetBackgroundBottomColor(0, 0, 0);

# Show logo
logo.image = Image("logo.png");
logo.sprite = Sprite(logo.image);
logo.x = Window.GetX() + Window.GetWidth() / 2 - logo.image.GetWidth() / 2;
logo.y = Window.GetY() + Window.GetHeight() / 2 - logo.image.GetHeight();
logo.sprite.SetPosition(logo.x, logo.y, 10000);

#-----------------------------------ProgressBar-------------------------------------
progressbar_bg.image = Image("progressbar_bg.png");                             
progressbar_bg.sprite = Sprite(progressbar_bg.image);
progressbar_bg.sprite.SetOpacity(0);                           
progressbar_bg.width = progressbar_bg.image.GetWidth();                         
progressbar_bg.offset_y = 10; 
progressbar_bg.x = Window.GetX() + Window.GetWidth() / 2 - progressbar_bg.image.GetWidth() / 2;
progressbar_bg.y = Window.GetX() + Window.GetHeight() / 2 + progressbar_bg.offset_y;
progressbar_bg.sprite.SetPosition(progressbar_bg.x, progressbar_bg.y, 0);          

progressbar_fg.y = progressbar_bg.y;                                            
progressbar_fg.width = progressbar_bg.width;                                    

progressbar_fg.image = Image("progressbar_fg.png");
progressbar_fg.sprite = Sprite(progressbar_fg.image);
progressbar_fg.sprite.SetOpacity(0);
progressbar_fg.x = progressbar_bg.x;
progressbar_fg.width = progressbar_fg.image.GetWidth();
progressbar_fg.sprite.SetPosition(progressbar_fg.x, progressbar_fg.y, 0);

is_forward = 1;
x_step = 5;
fun boot_callback() 
{
    progressbar_bg.sprite.SetOpacity(1);
    progressbar_fg.sprite.SetOpacity(1);
    if (progressbar_fg.x <= progressbar_bg.x) 
    {
        is_forward = 1;
    }
    if (progressbar_fg.x >= progressbar_bg.x + progressbar_bg.width - progressbar_fg.width) 
    {
        is_forward = 0;
    }
    if (is_forward) 
    {
        progressbar_fg.x += x_step;
    } 
    else 
    {
        progressbar_fg.x -= x_step;
    }
      
    progressbar_fg.sprite.SetPosition(progressbar_fg.x, progressbar_fg.y, 0);
}

reboot_opacity = 1.0;
fun reboot_callback() 
{
    if (reboot_opacity > 0.3) {
        reboot_opacity -= 0.005;
        logo.sprite.SetOpacity(reboot_opacity);
    }
}

if (Plymouth.GetMode() == "boot")
{
    Plymouth.SetRefreshFunction(boot_callback);
} 
else 
{
    Plymouth.SetRefreshFunction(reboot_callback);
}

#----------------------------------------- Dialogue --------------------------------

status = "normal";

fun dialog_setup()
  {
    local.box;
    local.lock;
    local.entry;
    
    box.image = Image("box.png");
    lock.image = Image("lock.png");
    entry.image = Image("entry.png");
    
    box.sprite = Sprite(box.image);
    box.x = Window.GetX() + Window.GetWidth()  / 2 - box.image.GetWidth ()/2;
    box.y = Window.GetY() + Window.GetHeight() / 2 - box.image.GetHeight()/2;
    box.z = 10000;
    box.sprite.SetPosition(box.x, box.y, box.z);
    
    lock.sprite = Sprite(lock.image);
    lock.x = box.x + box.image.GetWidth()/2 - (lock.image.GetWidth() + entry.image.GetWidth()) / 2;
    lock.y = box.y + box.image.GetHeight()/2 - lock.image.GetHeight()/2;
    lock.z = box.z + 1;
    lock.sprite.SetPosition(lock.x, lock.y, lock.z);
    
    entry.sprite = Sprite(entry.image);
    entry.x = lock.x + lock.image.GetWidth();
    entry.y = box.y + box.image.GetHeight()/2 - entry.image.GetHeight()/2;
    entry.z = box.z + 1;
    entry.sprite.SetPosition(entry.x, entry.y, entry.z);
    
    global.dialog.box = box;
    global.dialog.lock = lock;
    global.dialog.entry = entry;
    global.dialog.bullet_image = Image("bullet.png");
    dialog_opacity (1);
  }
    
fun dialog_opacity(opacity)
  {
    dialog.box.sprite.SetOpacity (opacity);
    dialog.lock.sprite.SetOpacity (opacity);
    dialog.entry.sprite.SetOpacity (opacity);
    for (index = 0; dialog.bullet[index]; index++)
      {
        dialog.bullet[index].sprite.SetOpacity(opacity);
      }
  }

fun display_normal_callback ()
  {
    global.status = "normal";
    if (global.dialog)
      dialog_opacity (0);
  }

fun display_password_callback (prompt, bullets)
  {
    global.status = "password";
    if (!global.dialog)
	dialog_setup();
    else
	dialog_opacity(1);
    for (index = 0; dialog.bullet[index] || index < bullets; index++)
      {
        if (!dialog.bullet[index])
          {
            dialog.bullet[index].sprite = Sprite(dialog.bullet_image);
            dialog.bullet[index].x = dialog.entry.x + index * dialog.bullet_image.GetWidth();
            dialog.bullet[index].y = dialog.entry.y + dialog.entry.image.GetHeight() / 2 - dialog.bullet_image.GetHeight() / 2;
            dialog.bullet[index].z = dialog.entry.z + 1;
            dialog.bullet[index].sprite.SetPosition(dialog.bullet[index].x, dialog.bullet[index].y, dialog.bullet[index].z);
          }
        if (index < bullets)
          dialog.bullet[index].sprite.SetOpacity(1);
        else
          dialog.bullet[index].sprite.SetOpacity(0);
      }
  }

Plymouth.SetDisplayNormalFunction(display_normal_callback);
Plymouth.SetDisplayPasswordFunction(display_password_callback);


#----------------------------------------- Message --------------------------------

message_sprite = Sprite();
message_sprite.SetPosition(Window.GetX() + 10, Window.GetY() + 10, 10000);

fun message_callback (text)
{
  my_image = Image.Text(text, 1, 1, 1);
  message_sprite.SetImage(my_image);
}

Plymouth.SetMessageFunction(message_callback);
